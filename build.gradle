apply(plugin: 'idea')

def springbootModule = project(':gabon-springboot')
springbootModule.ext.description = "Gabon com.cryfish.gabon.springboot"
springbootModule.ext.package = "com.cryfish.gabon.com.cryfish.gabon.springboot"

//versions
def spring_cloud_version = '2.3.4.RELEASE'
def commons_lang_version = '3.11'
def group = 'io.github.fishcancry'

version = System.getenv('RELEASE_VERSION') ?: "0.0.1"


def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        root.children().last() + {
            resolveStrategy = DELEGATE_FIRST

            name = project.name
            description = 'Cryfish common libraries for microservices'
            url = 'https://github.com/FishCanCry/gabon'
            organization {
                name 'com.github.fishcancry'
                url 'https://github.com/fishcancry'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/FishCanCry/gabon/issues'
            }
            licenses {
                license {
                    name 'The Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            scm {
                url 'https://github.com/FishCanCry/gabon'
                connection 'scm:https://github.com/FishCanCry/gabon.git'
                developerConnection 'scm:git://github.com/FishCanCry/gabon.git'
            }
            developers {
                developer {
                    id = 'chkmk'
                    name = 'Yuriy'
                    email = 'chkmk@gmail.com'
                }
            }
        }
    }
}

subprojects {
    ext.isSnapshot = !project.hasProperty('release')
    ext.app = 'gabon'

    repositories {
        mavenCentral()
    }

    apply(plugin: 'maven-publish')
    apply(plugin: 'java')
    apply(plugin: 'signing')

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                customizePom(pom)
                groupId group
                artifactId = project.name
                version version

                from components.java
            }
        }
        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }

    signing {
        sign publishing.publications
    }
}

configure([springbootModule]) {
    dependencies {
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: commons_lang_version
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: spring_cloud_version
    }
}